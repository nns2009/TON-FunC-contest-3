{-
	Contract receives internal message with text comment (https://ton.org/docs/develop/smart-contracts/guidelines/internal-messages) which contains arithmetic expression containing integer numbers in decimal representation and operations `(+-*/)`.
	All values (including intermediate) fit 256 bit. Contract should respond (coins = 0, mode = 64) with correct answer encoded as text comment back.
	It is guaranteed that all tests contain a valid algebraic equations.
	Division result should be rounded down. It is guaranteed that tests do not contain division by zero.
-}

int tuple_len(tuple t) asm "TLEN";
(tuple, int) ~tuple_pop(tuple t) asm "TPOP";


global int char;
global slice cs;


() next_char() impure inline {
	if (cs.slice_bits() < 8) {
		if (cs.slice_refs_empty?()) {
			char = 0; ;; expression end
			return ();
		}

		cs = cs.preload_ref().begin_parse();
	}

	;; This stupid indirection is required for FunC compiler to work
	(slice cs2, int char2) = cs.load_uint(8);
	cs = cs2;
	char = char2;
}

() print_char() impure {
	int temp = char;
	temp~dump();
	temp~impure_touch();
}

int expression() impure;

;; int item() { return 5; }
int item() impure {
	;; (<expression>)
	if (char == 40) { ;; '('
		next_char();
		int v = expression();
		next_char(); ;; skip ')';
		return v;
	}

	;; constant
	{
		int v = 0;
		while ((48 <= char) & (char <= 57)) { ;; between '0' and '9'
			int d = char - 48;
			v = v * 10 + d;
			next_char();
		}
		return v;
	}
}

;; int group() { return 111; }
int group() impure {
	int v = item();

	while ((char == 42) | (char == 47)) {
		if (char == 42) { ;; '*'
			next_char();
			v *= item();
		} else { ;; '/'
			next_char();
			v /= item();
		}
	}

	return v;
}

int expression() impure {
	int v = group();

	while ((char == 43) | (char == 45)) {
		if (char == 43) { ;; '+'
			next_char();
			v += group();
		} else { ;; '-'
			next_char();
			v -= group();
		}
	}

	return v;
}

builder print_int(builder b, int v) {
	int is_zero = v == 0;

	if (v < 0) {
		b~store_uint(45, 8);
		v = - v;
	}

	tuple t = empty_tuple();
	if (is_zero) {
		t~tpush(0);
	} else {
		while (v > 0) {
			int d = v % 10;
			v /= 10;
			t~tpush(d);
		}
	}

	;; t~dump();
	repeat (t.tuple_len()) {
		int d = t~tuple_pop();
		b~store_uint(d + 48, 8);
	}

	return b;
}

;; testable
() recv_internal (cell message, slice in_msg_body) {
	slice header = message.begin_parse();
	header~skip_bits(4);
	slice sender = header~load_msg_addr();

	cs = in_msg_body.skip_bits(32); ;; op=0
	next_char();
	;; print_char();

	int res = expression();

	var msg = begin_cell()
		.store_uint(0x18, 6)
		.store_slice(sender)
		.store_coins(0)
		.store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1)
		.store_uint(0, 32) ;; comment message
		.print_int(res)
		.end_cell();
	send_raw_message(msg, 64);
}
